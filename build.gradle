plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'org.nitramproductions.com'
version '1.1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("src/main/resources/org/nitramproductions/com/wordclassifier/controller/properties/version.properties").withWriter { writer ->
            Properties properties = new Properties()
            properties['version'] = project.version.toString()
            properties.store writer, null
        }
    }
}

tasks.register('jpackageDistribution', Zip) {
    archiveFileName = project.name + "-" + project.version + ".zip"
    destinationDirectory = layout.buildDirectory.dir("distributions")
    dependsOn(jpackage)

    from layout.buildDirectory.dir("jpackage/WordClassifier")
}

application {
    mainModule = 'org.nitramproductions.com.wordclassifier'
    mainClass = 'org.nitramproductions.com.wordclassifier.MainApplication'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

classes {
    dependsOn createProperties
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.0')
    implementation('com.dlsc.formsfx:formsfx-core:11.3.2') {
        exclude(group: 'org.openjfx')
    }
    implementation('net.synedra:validatorfx:0.2.1') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.2.0')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('eu.hansolo:tilesfx:11.48') {
        exclude(group: 'org.openjfx')
    }
    implementation 'com.h2database:h2:2.0.202'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = project.name
    }
    jpackage {
        imageOptions = [
                '--icon', 'src/main/resources/org/nitramproductions/com/wordclassifier/book-icon.ico',
                '--app-version', project.version
        ]
        installerOptions = [
                '--type', 'msi',
                '--description', 'WordClassifier Windows Software',
                '--copyright', 'Copyright 2021 NitramProductions.com',
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'NitramProductions',
                '--win-shortcut',
                '--win-upgrade-uuid', '89b2dc65-7188-4876-9e8e-04907c2b609e'
        ]
    }
}

jlinkZip {
    group = 'distribution'
}